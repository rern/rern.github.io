# Maintainer: AudioLinux  <audiolinux AT fastmail DOT fm>
# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Mansour Behabadi <mansour@oxplot.com>
# Contributor: Tilman Vatteroth <tilman.vatteroth@udo.edu>

pkgname=upmpdcli
pkgver=1.8.11
pkgrel=1
pkgdesc="A UPnP Media Renderer front-end for the Music Player Daemon (MPD)"
arch=(i686 x86_64 arm armv7h armv6h aarch64)
url="https://www.lesbonscomptes.com/upmpdcli/"
license=(GPL2)
depends=(python libmpdclient libupnpp libmicrohttpd jsoncpp)
makedepends=(python-setuptools python-requests python-bottle python-mutagen aspell-en id3lib python-waitress sqlite)
optdepends=(aspell-en id3lib sqlite
            'python-requests: enable search'
            'python-bottle: enable uprcl media server'
            'python-mutagen: enable uprcl media server'
            'python-waitress: enable uprcl media server'
            'recoll: enable uprcl media server')
install=upmpdcli.install
source=("http://www.lesbonscomptes.com/upmpdcli/downloads/$pkgname-$pkgver.tar.gz"
        'upmpdcli.service')
sha256sums=('55e3d27bea5bef4560f319137f1a5c7d08abbba5a9aae90ee3240903fdb8c3e4'
            '53da5e4e40a1987e3c137270b315f8c0ba9521539c563e75e254f48449ce0ae9')
backup=('etc/upmpdcli.conf')

build(){
	cd $pkgname-$pkgver
	mkdir -p build
	arch-meson build
	meson compile -C build
}

package(){
	cd $pkgname-$pkgver
	meson install -C build --destdir $pkgdir
	install -Dm644 -t $pkgdir/usr/lib/systemd/user/ $srcdir/upmpdcli.service 
	chmod 644 $pkgdir/etc/upmpdcli.conf
}
