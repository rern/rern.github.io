# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Greg Erwin <first name last name 256 at gmail dot com>

pkgname=libgpiod
pkgver=2.0.1
pkgrel=1
pkgdesc="C library and tools for interacting with the linux GPIO character device"
url="https://git.kernel.org/pub/scm/libs/libgpiod/libgpiod.git"
arch=('x86_64')
license=('LGPL2.1')
depends=('python')
makedepends=('autoconf-archive' 'doxygen' 'help2man' 'python-setuptools')
conflicts=('libgpiod-git')
source=("$url/snapshot/$pkgname-$pkgver.tar.gz")
sha256sums=('b6eda55356160a8e73906e3d48e959ef81296787d764975b10f257e9660668e9')

prepare() {
  # Fix python module installation path, see https://gitlab.archlinux.org/archlinux/packaging/packages/libgpiod/-/issues/1
  cd $pkgname-$pkgver
  sed -i -e 's|--prefix=\$(DESTDIR)\$(prefix)|--prefix=$(prefix) --root=$(if $(DESTDIR),$(DESTDIR),/)|g' bindings/python/Makefile.am
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh \
    --prefix=/usr \
    --enable-tools=yes \
    --enable-bindings-cxx \
    --enable-bindings-python
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
